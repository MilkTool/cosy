// the basics of cosy

// print to output
print "hello world!"


// variables and types
var a = true     // Boolean
var b = 1        // Number
var c = "hello"  // String

// variables are immutable by default
var d = 42
// d = 24 // error

// use the `mut` keyword to create a mutable variable
mut e = 42
e = 24 // ok


// conditional branching
if 3 < 5 and 4 == 4 {
    print "true"
} else {
    print "false"
}


// loop with counter
for i in 0..10 {
    print "loop #" + i
}

// loop with condition
mut j = 0
for j < 10 {
    print "conditional loop #i"
    j = j + 1
}


// functions
fun say(name) {
    print "hello " + name
}
say("world")

// lambda functions
var square = fun(value) {
    return value * value
}
print "5 * 5 = " + square(5)

// functions can return functions
fun say_with_extra_text(extra_text) {
    return fun(text) {
        print text + extra_text
    }
}
var print_courteously = say_with_extra_text(", please!")
print_courteously("make me a sandwich")

// functions can tage functions as arguments
fun do_n_times(f, n) {
    for i in 0..n+1 {
        f(i)
    }
}
do_n_times(fun(x) { print "i'm called " + x + " time(s)" }, 3)

// functions act as closures
fun counter(start, increment) {
    mut count = start
    return fun() {
        count = count + increment
        return count
    }
}
print "counting down..."
var count_down = counter(3, -1)
print count_down()
print count_down()
print count_down()


// dead code is not allowed
fun dead_code() {
    print "i'm always printing this"
    return true
    // print "i'm never printing this" // error
}
dead_code()


// shadowing of variables are not allowed
{
    var unique = true
    {
        //var unique = 3 // error
    }
 	print unique
}


// unused variables are not allowed
var unused = 42 // error if the line below is removed
print unused
// because of this, we also have to use the variables defined at the top
if !a print b + c + d + e

// variables can be marked purposely unused with an underscore
var _unused = 42 // this is okay
for _i in 0..2 {
    print "the counter variable is unused, but that's okay"
}


// that's it for the basics
// for more examples see:
///https://github.com/anissen/cosy/tree/master/test/scripts
