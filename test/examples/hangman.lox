var word_to_guess;
var rnd = random();
if (rnd < 0.1)      word_to_guess = "TABLECLOTH";
else if (rnd < 0.2) word_to_guess = "WATERMELON";
else if (rnd < 0.3) word_to_guess = "COMPUTER";
else if (rnd < 0.4) word_to_guess = "BICYCLE";
else if (rnd < 0.5) word_to_guess = "GARFIELD";
else if (rnd < 0.6) word_to_guess = "SAFEPILOT";
else if (rnd < 0.7) word_to_guess = "PAPERCUT";
else if (rnd < 0.8) word_to_guess = "SUNGLASSES";
else if (rnd < 0.9) word_to_guess = "AWESOME";
else if (rnd < 1.0) word_to_guess = "RAINBOW";
var word_length = str_length(word_to_guess);
var letters_guessed = "";
var guessed_length = 0;
var wrong_guesses_remaining = 5;

fun has_guessed_letter(letter) {
    var guessed_letters_length = str_length(letters_guessed);
    for (var i = 0; i < guessed_letters_length; i = i + 1) {
      if (str_charAt(letters_guessed, i) == letter) {
        return true;
      }
    }
    return false;
  }

fun get_display_word() {
  var word_display = "";
  for (var i = 0; i < word_length; i = i + 1) {
    var letter = str_charAt(word_to_guess, i);
    if (has_guessed_letter(letter)) {
      word_display = word_display + letter + " ";
    } else {
      word_display = word_display + "_ ";
    }
  }
  return word_display;
}

fun has_won() {
  for (var i = 0; i < word_length; i = i + 1) {
    var letter = str_charAt(word_to_guess, i);
    if (!has_guessed_letter(letter)) {
      return false;
    }
  }
  return true;
}

fun hangman() {
  print "You need to guess '" + get_display_word() + "'!";
  print "Wrong guesses remaining: " + wrong_guesses_remaining;
  print "-------------------------";
  print "Guess: ";
  var letter = input();
  print "";

  if (str_length(letter) != 1) {
    print "'" + letter + "' is not a valid answer!";
    return hangman();
  }

  fun already_guessed() {
    var length = str_length(letters_guessed);
    for (var i = 0; i < length; i = i + 1) {
      if (str_charAt(letters_guessed, i) == letter) {
        print "You already guessed that!";
        wrong_guesses_remaining = wrong_guesses_remaining - 1;
        return true;
      }
    }
    return false;
  }
  
  if (already_guessed()) {
    return hangman();
  }

  letters_guessed = letters_guessed + letter;

  fun is_correct_guess() {
    for (var i = 0; i < word_length; i = i + 1) {
      if (str_charAt(word_to_guess, i) == letter) {
        return true;
      }
    }
    return false;
  }

  if (is_correct_guess()) {
    print "Correct!";
    print "";
  } else {
    print "Wrong guess!";
    print "";
    wrong_guesses_remaining = wrong_guesses_remaining - 1;
    if (wrong_guesses_remaining <= 0) {
      print "Game over: You lost!";
      print "The correct word was " + word_to_guess;
      return;
    }
  }

  if (has_won()) {
    print "The correct word is " + word_to_guess;
    print "Game over: You won!";
    return;
  }

  hangman();
}

print "Welcome to Hangman!";
print "";

hangman();

print "";
print "Thanks for playing :)";
